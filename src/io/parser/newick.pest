WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

// grammar rules from https://en.wikipedia.org/wiki/Newick_format

numeric = {"-"? ~ (ASCII_DIGIT | ".")+ ~ ("e" ~"-"? ~(ASCII_DIGIT | ".")+)? }
branchlength = {("-"|"+")? ~ (ASCII_DIGIT | "." | "-"|^"e")+}
// branchlength = @{numeric}

length = {":" ~ branchlength}
valid_name_char = {!( "="|"("|")"|"["|"]"|"{"|"}"|","| ";" | ":"| WHITE_SPACE| "\'" ) ~ ANY}
unquoted_name ={valid_name_char+}
inner={!("\'")~ANY}
quoted_name = {"'" ~ inner+ ~ "'" }
name = @{(quoted_name|unquoted_name)}

leaf = @{name}

branch= {subtree ~node_annotation? ~length?}

subtree = {internal|leaf}
internal = {"("~branchset ~")"~name?}

branchset = { (branch ~ "," ~ branchset) | branch}

tree = { WHITE_SPACE*~(subtree ~ ";" | branch ~ ";")~WHITE_SPACE* }

//TODO add label and support for name in parser rules

label = {continuous| name }
node_annotation={"[&"~annotation_set~"]"}
annotation = {key~"="~value}
key=@{name}
value = {continuous|set|discrete}
continuous = {(ASCII_DIGIT | "." | "-"|^"e")+}
discrete = @{name}
one_entry={continuous|discrete}
set={"{" ~one_entry ~ (","~one_entry)* ~"}"}
annotation_set = {(annotation~","~annotation_set) | annotation}

